package Email::VirusScan::Engine::Trend::Vscan;
use strict;
use warnings;
use Carp;

use Email::VirusScan::Engine;
use vars qw( @ISA );
@ISA = qw( Email::VirusScan::Engine );

use Cwd 'abs_path';

use Email::VirusScan::Result;

sub new
{
	my ($class, $conf) = @_;

	if( ! $conf->{command} ) {
		croak "Must supply a 'command' config value for $class";
	}

	my $self = {
		command => $conf->{command},
		args    => [ '-za', '-a' ],
	};

	return bless $self, $class;
}

sub scan_path
{
	my ($self, $path) = @_;

	if( abs_path($path) ne $path ) {
		return Email::VirusScan::Result->error( "Path $path is not absolute" );
	}

	my ($exitcode, $scan_response) = eval {
		$self->_run_commandline_scanner(
			join(' ', $self->{command}, @{$self->{args}}, $path, '2>&1'),
			'Found',
		);
	};

	if( $@ ) {
		return Email::VirusScan::Result->error( $@ );
	}

	if( 0 == $exitcode ) {
		return Email::VirusScan::Result->clean();
	}

	if( $exitcode >= 1 &&
	    $exitcode <  10 ) {
		my ($virus_name) = $scan_response =~ m/^\*+ Found virus (\S+)/;
		$virus_name ||= 'unknown-Trend-virus';
		return Email::VirusScan::Result->virus($virus_name);
	}

	return Email::VirusScan::Result->error("Unknown return code: $exitcode");
}

1;
__END__

=head1 NAME

Email::VirusScan::Engine::Trend::Vscan - Email::VirusScan backend for scanning with Trend Micro vscan

=head1 SYNOPSIS

    use Email::VirusScanner;
    my $s = Email::VirusScanner->new({
	engines => {
		'-Trend::Vscan' => {
			command => '/path/to/fprot',
		},
		...
	},
	...
}

=head1 DESCRIPTION

Email::VirusScan backend for scanning using Trend's commandline scanner

This class inherits from, and follows the conventions of,
Email::VirusScan::Engine.  See the documentation of that module for
more information.

=head1 CLASS METHODS

=head2 new ( $conf )

Creates a new scanner object.  B<$conf> is a hashref containing:

=over 4

=item command

Required.

Path to scanner executable.

=back

=head1 INSTANCE METHODS

=head2 scan_path ( $pathname )

Scan the path provided using the configured command.

Returns an Email::VirusScan::Result object.

=head1 DEPENDENCIES

L<Cwd>, L<Email::VirusScan::Result>,

=head1 AUTHOR

Dave O'Neill (dmo@roaringpenguin.com)

=head1 LICENCE AND COPYRIGHT

Copyright (c) 2007 Roaring Penguin Software, Inc.

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl itself.
